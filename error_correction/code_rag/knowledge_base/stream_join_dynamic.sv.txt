[Keyword]: stream_join_dynamic.sv

[Design Category]: Interface Circuits

[Design Function Description]:
The design is a stream joiner that dynamically combines multiple input streams into a single output stream. It uses a valid-ready handshake protocol, similar to AXI4, to manage the flow of data. The selection of which input streams to join is controlled dynamically by a selection mask (`sel_i`). The output stream is only valid when there is a valid handshake on the selected input streams.

[Input Signal Description]:
- `inp_valid_i[N_INP-1:0]`: An array of valid signals for each input stream, indicating whether the data on the corresponding input stream is valid.
- `sel_i[N_INP-1:0]`: A selection mask that determines which input streams are to be joined and considered for the output stream. Each bit corresponds to an input stream.
- `oup_ready_i`: A ready signal for the output stream, indicating whether the downstream component is ready to accept data.

[Output Signal Description]:
- `inp_ready_o[N_INP-1:0]`: An array of ready signals for each input stream, indicating whether the module is ready to accept data from the corresponding input stream.
- `oup_valid_o`: A valid signal for the output stream, indicating that the output data is valid and can be consumed by the downstream component.


[Design Detail]:
// Copyright 2020 ETH Zurich and University of Bologna.
// Copyright and related rights are licensed under the Solderpad Hardware
// License, Version 0.51 (the "License"); you may not use this file except in
// compliance with the License. You may obtain a copy of the License at
// http://solderpad.org/licenses/SHL-0.51. Unless required by applicable law
// or agreed to in writing, software, hardware and materials distributed under
// this License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

// Authors:
// - Luca Colagrande <colluca@iis.ee.ethz.ch>

`include "common_cells/assertions.svh"

// Stream join dynamic: Joins a parametrizable number of input streams (i.e. valid-ready
// handshaking with dependency rules as in AXI4) to a single output stream. The subset of streams
// to join can be configured dynamically via `sel_i`. The output handshake happens only after
// there has been a handshake. The data channel flows outside of this module.
module stream_join_dynamic #(
  /// Number of input streams
  parameter int unsigned N_INP = 32'd0 // Synopsys DC requires a default value for parameters.
) (
  /// Input streams valid handshakes
  input  logic [N_INP-1:0] inp_valid_i,
  /// Input streams ready handshakes
  output logic [N_INP-1:0] inp_ready_o,
  /// Selection mask for the output handshake
  input  logic [N_INP-1:0] sel_i,
  /// Output stream valid handshake
  output logic             oup_valid_o,
  /// Output stream ready handshake
  input  logic             oup_ready_i
);

  // Corner case when `sel_i` is all 0s should not generate valid
  assign oup_valid_o = &(inp_valid_i | ~sel_i) && |sel_i;
  for (genvar i = 0; i < N_INP; i++) begin : gen_inp_ready
    assign inp_ready_o[i] = oup_valid_o & oup_ready_i & sel_i[i];
  end

`ifndef COMMON_CELLS_ASSERTS_OFF
  `ASSERT_INIT(n_inp_0, N_INP >= 1, "N_INP must be at least 1!")
`endif
endmodule
