[Keyword]: mv_filter.sv

[Design Category]: Sequential Logic

[Design Function Description]:
The design is a moving average filter with a threshold mechanism. It counts the number of consecutive high input samples (`d_i`) and sets the output (`q_o`) high if the count reaches a specified threshold. The counter can be reset asynchronously with `rst_ni` or synchronously with `clear_i`.

[Input Signal Description]:
- `clk_i`: Clock input signal for synchronizing the sequential logic.
- `rst_ni`: Active-low asynchronous reset signal to initialize the counter and output.
- `sample_i`: Signal indicating when to sample the input `d_i`.
- `clear_i`: Signal to synchronously clear the counter and reset the output.
- `d_i`: Input data signal to be sampled and counted.

[Output Signal Description]:
- `q_o`: Output signal that goes high when the count of consecutive high samples reaches the threshold.


[Design Detail]:
// Copyright 2018 ETH Zurich and University of Bologna.
// Copyright and related rights are licensed under the Solderpad Hardware
// License, Version 0.51 (the "License"); you may not use this file except in
// compliance with the License. You may obtain a copy of the License at
// http://solderpad.org/licenses/SHL-0.51. Unless required by applicable law
// or agreed to in writing, software, hardware and materials distributed under
// this License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

// Author: Florian Zaruba <zarubaf@iis.ee.ethz.ch>

module mv_filter #(
    parameter int unsigned WIDTH     = 4,
    parameter int unsigned THRESHOLD = 10
)(
    input  logic clk_i,
    input  logic rst_ni,
    input  logic sample_i,
    input  logic clear_i,
    input  logic d_i,
    output logic q_o
);
    logic [WIDTH-1:0] counter_q, counter_d;
    logic d, q;

    assign q_o = q;

    always_comb begin
        counter_d = counter_q;
        d = q;

        if (counter_q >= THRESHOLD[WIDTH-1:0]) begin
            d = 1'b1;
        end else if (sample_i && d_i) begin
            counter_d = counter_q + 1;
        end

        // sync reset
        if (clear_i) begin
            counter_d = '0;
            d = 1'b0;
        end
    end

    always_ff @(posedge clk_i or negedge rst_ni) begin
        if (~rst_ni) begin
            counter_q <= '0;
            q         <= 1'b0;
        end else begin
            counter_q <= counter_d;
            q         <= d;
        end
    end
endmodule
